$.fn.extend
  initialize: ->
    $('.viewport', this).each ->
      $(this).initViewport()

    $('.gallery-wrap', this).each ->
      $(this).initGallery()

    $('.gallery', this).each ->
      $(this).magnificPopup
        delegate: '.image-link'
        type: 'image'
        image:
          titleSrc: 'title'
        gallery:
          enabled: true
          tPrev: '←'
          tNext: '→'
          tCounter: '%curr%/%total%'

    $('[data-toggle="tooltip"]', this).tooltip()
    $('[data-toggle="popover"]', this).popover
      html: true
      trigger: 'hover'

    $('.slider', this).nouislider()
    $('.selectize', this).betterSelectize()

    $('input.color', this).minicolors
      theme: 'bootstrap'

    $('.column[data-reorder]', this).each ->
      $(this).column_sortable()

    $('.infinite-table', this).infinitePages
      loading: ->
        $('#spinner').show()

  deactivate: ->
    $(this).removeClass 'active'
  activate: ->
    $(this).addClass 'active'
    $(this).parents('li').addClass 'active'

  make_room: (element) ->
    $(this).css 'padding-bottom', element.height()

  masonry_build: ->
    $grid = $('.staggered-grid', this)
    $grid.imagesLoaded ->
      $grid.masonry
        itemSelector: '.grid-item'
        columnWidth: '.single'
        percentPosition: true
        transitionDuration: 0

$(document).on 'keypress', 'form.immediate .form-control', (e) ->
  if e.key is 'Enter'
    e.preventDefault()
    $(e.currentTarget).trigger 'submit.rails'

$(document).on 'change', 'form.immediate', (e) ->
  $(e.currentTarget).trigger 'submit.rails'

$(document).on 'click', '[data-toggle=sidebar]', (e) ->
  $('.sidebar').toggleClass 'active'

$(document).on 'click', 'a[href="#back"]', (e) ->
  e.preventDefault()
  history.back()

$(document).on 'click', 'a[href="#print"]', (e) ->
  e.preventDefault()
  $.print()

jQuery ->

  $(document).initialize()

  $('#side-nav-menu').metisMenu()
  .on 'shown.metisMenu', (e) ->
    $(e.target).parent().scrollMenu $('#side-nav')
  $('#side-nav-menu').find('.active').scrollMenu $('#side-nav')

  $('#content-wrap').imagesLoaded ->
    selector = '#main-nav .nav > .scroll > a'
    if !!document.querySelectorAll(selector).length
      new SmoothScroll selector
      new Gumshoe selector, reflow: true

  $('.products').masonry_build()

  new Clipboard('.clipboard')
