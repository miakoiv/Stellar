$.fn.extend
  initialize: ->

    viewportUpdate()

    $('.gallery', this).each ->
      $(this).magnificPopup
        delegate: '.image-link'
        type: 'image'
        gallery:
          enabled: true

    $('[data-toggle="tooltip"]', this).tooltip()
    $('[data-toggle="popover"]', this).popover
      html: true
      trigger: 'hover'

    $('.selectize', this).selectize
      plugins: ['remove_button']
      dropdownParent: 'body'
      allowEmptyOption: true

    $('.infinite-table', this).infinitePages
      loading: ->
        $('#spinner').show()

    $('.credit-card-number').payment 'formatCardNumber'
    $('.credit-card-expiry').payment 'formatCardExpiry'
    $('.credit-card-cvc').payment 'formatCardCVC'

  deactivate: ->
    $(this).removeClass 'active'
  activate: ->
    $(this).addClass 'active'
    $(this).parents('li').addClass 'active'

  make_room: (element) ->
    $(this).css 'padding-bottom', element.height()

  parallax_refresh: ->
    $('.parallax-mirror').remove()
    $parallax = $('.parallax-window')
    $parallax.imagesLoaded ->
      $parallax.parallax($parallax.data())

  masonry_build: ->
    $masonry = $('.masonry-grid', this)
    $masonry.imagesLoaded ->
      $masonry.masonry
        itemSelector: '.masonry-item'
        columnWidth: '.single'
        percentPosition: true
      if window.wiselinks? then Wiselinks.scrollPage()

  masonry_refresh: ->
    $masonry = $('.masonry-grid', this)
    $masonry.masonry()

jQuery ->

  $(document).initialize()
  $('.products').masonry_build()

  new Clipboard('.clipboard')

  $(document).on 'change', 'form.immediate', (e) ->
    $(e.currentTarget).trigger('submit.rails')

  $(document).on 'click', '[data-toggle=sidebar]', (e) ->
    $('.sidebar').toggleClass 'active'

  $(document).on 'click', 'a[href="#back"]', (e) ->
    e.preventDefault()
    history.back()

  $(document).on 'click', 'a[href="#print"]', (e) ->
    e.preventDefault()
    $.print()
