$.fn.extend
  initialize: ->

    $('.gallery', this).each ->
      $(this).magnificPopup
        delegate: '.image-link'
        type: 'image'
        gallery:
          enabled: true

    $('[data-toggle="tooltip"]', this).tooltip()
    $('[data-toggle="popover"]', this).popover
      html: true
      trigger: 'hover'

    $('.selectize', this).selectize
      plugins: ['remove_button']
      dropdownParent: 'body'
      allowEmptyOption: true

    $('.nav-tabs[role="tablist"]').stickyTabs
      backToTop: true

  deactivate: ->
    $(this).removeClass 'active'
  activate: ->
    $(this).addClass 'active'
    if parent = $(this).data 'parent'
      $(parent).activate()

  rebuild_masonry: ->
    $brickwork = $('.brickwork')
    $brickwork.masonry('destroy') if $brickwork.hasClass('masonry')
    $brickwork.imagesLoaded ->
      this.masonry
        itemSelector: 'li.product'

jQuery ->

  window.wiselinks = new Wiselinks()

  $(document).initialize()
  $(document).rebuild_masonry()

  $(document).on 'change', 'form.immediate', (e) ->
    $(e.currentTarget).trigger('submit.rails')

  $(document).on 'click', '[data-toggle=sidebar]', (e) ->
    $('.sidebar').toggleClass 'active'

  $(document).on 'click', 'a[href="#back"]', (e) ->
    e.preventDefault()
    history.back()

  $(document).on 'click', 'a[href="#print"]', (e) ->
    e.preventDefault()
    $.print()

  $(document).on 'show.bs.modal', '.modal', (e) ->
    $('.modal-body', this).css 'height', $(window).height() - 120

  $(document).on 'page:loading', -> NProgress.start()
  $(document).on 'page:always', -> NProgress.done()

  $(document).on 'page:done', (event, $target, status, url, data) ->
    $(document).rebuild_masonry()
    if ga? then ga('send', 'pageview', window.location.pathname)
